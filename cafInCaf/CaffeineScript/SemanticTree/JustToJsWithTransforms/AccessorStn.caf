import &StandardImport

class AccessorStn extends &AccessorChainStn

  constructor: (props, children) ->
    super
    throw new Error "2 children" unless children.length == 2
    @value = children[0]
    @key = children[1]
    throw new Error "need key" unless @key

  needsParens: false

  @getter
    existanceTest: -> @props.existanceTest
    isAccessor: -> true

  toJs: ->
    base = @value.toJsExpression dotBase: true
    if @key.isIdentifier
      if (identifierString = @key.toJs()).match /['"`]/
        "#{base}[#{identifierString}]"
      else
        "#{base}.#{identifierString}"
    else
      "#{base}[#{@key.toJsExpression()}]"

  toSourceNode: ->
    base = @value.toSourceNode expression: true dotBase: true

    if @key.isIdentifier
      @createSourceNode
        base
        "."
        @key.toSourceNode()

    else
      @createSourceNode
        base
        "["
        @key.toSourceNode()
        "]"
