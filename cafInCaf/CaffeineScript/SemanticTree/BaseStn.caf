import &StandardImport
{createObjectTreeFactory} = &ArtObjectTreeFactory
{SourceNode} = &SourceMap
{binary} = &ArtBinary

##
  TODO:
    I want all Stn constructors to only accept an object.
    All children are labeled children.
    Pros
    - Positional is so fragile.
    - Property values can be children or arrays of children (for unbounded lists, where order matters)
    - It'll be much easier for writing code that generates Stns

class BaseStn extends BaseClass
  ################################################
    Could go in CaffeineEight standard BaseStn
  ################################################

  @abstractClass()

  ###
    IN:
      props should be a plain-object-structure with an object as the root
      children should be a compatoJsParenExpressionct, flat array of Stns

    TODO: I'd like to make setting the parseTreeNode cleaner.
  constructor: (props, @children = [], @pretransformedStn) ->
    super
    @parseTreeNode = @pretransformedStn?.parseTreeNode ? props.parseTreeNode
    # throw new Error "no @parseTreeNode (#{@type})" unless @parseTreeNode
    @pretransformedStn ||= @

    @props = objectWithout props, "parseTreeNode"
    @initLabeledChildren()

  initLabeledChildren: ->
    @labeledChildren = @children && {}
    each child in @children
      child.parent = @
      {label, pluralLabel} = child
      @labeledChildren[label] = child
      if pluralLabel
        (@labeledChildren[pluralLabel] ||= []).push child

  getInspectedProps: ->
    merge
      objectWithout @props, "label", "pluralLabel"
      parseTreeNode: !!@parseTreeNode

  @getter
    sourceOffset: -> @parseTreeNode.offset
    parser: -> @parseTreeNode.parser.rootParser
    sourceFile: -> @parser.sourceFile
    label: -> @props.label
    pluralLabel: -> @props.pluralLabel
    root: -> @parent?.root ? @
    inspectedObjects: ->
      {label} = @

      props = @getInspectedProps()
      name = @class.getName()
      name = "#{label}.#{name}" if label

      "#{name}":
        if @children.length == 0
          toInspectedObjects props
        else
          a = []
          a.push props if objectKeyCount(props) > 0
          a.concat array c in @children with c.inspectedObjects

    type: -> @class.type

  @postCreate: ->
    s = @getName().split /Stn$/
    @type = s[0]
    super

  # so subclasses can add custom newInstance implementations
  @newInstance: (props, children) ->
    new @ props, children

  @postCreateConcreteClass: (options)->
    {classModuleState, hotReloadEnabled} = options
    super
    &StnRegistry.register createObjectTreeFactory
      class: @
      (props, children) =>
        @newInstance props, children

  findParent: (stnTypePattern) ->
    {parent} = @
    found = null
    while parent && !found
      if parent.type.match stnTypePattern
        found = parent
      else
        {parent} = parent
    found

    ## TODO - with "tap", this is just:
      {parent} = @
      while parent
        if parent.type.match stnTypePattern
          parent tap parent = null
        else
          parent extract parent

      # - with "return", this is just:
      {parent} = @
      while parent
        if parent.type.match stnTypePattern
          return parent
        else
          parent extract parent

      # recursion:
      if parent.type.match stnTypePattern
        parent
      else
        parent.findParent stnTypePattern

  find: (stnTypePattern) ->
    a = compactFlatten array child in @children
      if child.type.match stnTypePattern
        child
      else
        child.find stnTypePattern

    if a.length == 0
      null
    else
      a

  ############################################
    Unique to CafScript
  ############################################

  ##########################
    NEW CODE GENERATION
  ##########################
    New code-gen generates "SourceNodes" from the 'source-map' npm.

    https://www.npmjs.com/package/source-map

  sourceNodeLineColumnScratch = {}
  @getter
    sourceFile: -> @parseTreeNode?.sourceFile ? "caffeine-script"
    newSourceNode: ->
      # both are 0-base
      {line, column} = @parseTreeNode.getSourceLineColumn sourceNodeLineColumnScratch

      new SourceNode
        line + 1  # should be 1-based
        column    # should be 0-based
        @parseTreeNode.sourceFile

  # options are the same options passed to "toJs" - see below
  toSourceNode: (options) ->
    log.warn "WARNING: toSourceNode not overridden in #{@class.name}. Falling back to old toJs()."
    @newSourceNode.add @toJs options

  toJsWithInlineSourceMap: (options = {}) ->
    {code, map} =
      @toSourceNode options
      .toStringWithSourceMap file: options?.outputFile

    if options.verbose
      log {sourceMap:map.toString(),@sourceFile}

    """
      #{code}
      //# sourceMappingURL=#{binary(JSON.stringify map.toString()).toDataUri :application/json, true}
      //# sourceURL=#{@sourceFile}

  null
    # hack-fix for caf-script's current but of comments-getting-sucked-up-into
    # immediately proceeding block-strings. see: tests-to-add.txt for details
  ##########################
    OLD CODE GENERATION
  ##########################

  ##
    IN: options:
      expression: t/f
        if true, return JS for an expression

      subExpression: t/f
        currently this means the result is used
        in a binary operator

      statement: t/f
        true: is used as a statement

      dotBase: t/f
        true: is used as the base for a "." or "[]" accessor.

      returnValueIsIgnored: t/f
        if true and expression is true, still return
        an expression, but it's OK to streamline since the
        return-value is ignored.

        This is for use in expressiong-statement-lists: (expressionStatement1, es2, es3)
        JavaScript requires they be expressions, but it ignores the return value of all
        but the last one in the list, in this example, "es3".

  toJs: (options) ->
    throw new Error "must override one of the toJs* functions: #{@className}"

  childrenToJs: (joiner = '', options)->
    array c in @children with c.toJs options
    .join joiner

  doJs: (args, body) ->
    throw "TODO" if args
    body = body.toFunctionBodyJs() unless isString body

    "(() => {#{body};})()"

  toFunctionBodyJsArray: (returnAction = true)->
    if returnAction
      ["return #{@toJsExpression()}"]
    else
      [@toJs()]

  toFunctionBodyJs: (returnAction = true)->
    @toFunctionBodyJsArray(returnAction).join ''

  # return JS code that can be used as js-expression (returns a value)
  # for statements: a; b; return c;
  toJsExpression: (options)->
    @toJs merge options, expression: true

  # a string that can be inserted in the middle of an ES6 interpolation string: `...`
  toInterpolatedJsStringPart: -> "${Caf.toString(#{@toJsExpression()})}"

  ##########################
  ### TRANSFORM
  ##########################
  ##
    TODO: this is getting out of hand! We need to convert
    the whole SematicTree system over to ONLY using labeled children.

    In fact, CaffeineEight should probably strongly discourage using the "@matches"
    list.

    Labeled children can either be singles or arrays - if more than one child
    has the same label.

    Labels from CaffeineEight should be explicit labels, if there are any, OR
    the rule's name. I think right now we have a 'contextual' aspect which is
    just confusing - if any node in a chain of nodes has a label, that's what
    is used for the label for that branch.

    We should also refactor BaseStn so that transform ALWAYS
    transforms children first, then, optionally, the transform override is called,
    after the children are 'normalized.'

    Rethinking transform:

      I think I want two node-types: 'macros' and 'standard'

      standard: can output JS and are more-or-less 1:1 with JS.
        Auto-lets will probably still be managed by standard-nodes,
        but otherwise, the output-JS requires no other preprocessing.

        These nodes do not have a 'transform' method.

      macros:
        These nodes cannot generate JS, but they can transform themselves
        into 'standard' nodes.

        Macro nodes expect their children to be standard nodes when
        their transform-method is called.

  transformChildren: ->
    ret = null
    each child, i in @children
      if child != newChild = child.transform()
        ret ?= @children.slice()
        newChild.props.label = child.label
        ret[i] = newChild
    ret || @children

  # transform after children have been transformed
  postTransform: -> @

  newTransformedInstance: (newProps, newChildren) ->
    new @class newProps, newChildren, @

  transform: ->
    if @children != newChildren = @transformChildren()
      @newTransformedInstance @props, newChildren
    else
      @
    .postTransform()
    .setDefaultParseTreeNode @parseTreeNode

  setDefaultParseTreeNode: (parseTreeNode)->
    unless @parseTreeNode
      @parseTreeNode = parseTreeNode
      each child in @children
        child.setDefaultParseTreeNode parseTreeNode
    @

  ##########################
    PARENTHESIS
  ##########################
  needsParens: true
  needsParensAsStatement: false

  getNeedsParens: -> @needsParens
  getNeedsParensAsStatement: -> @needsParensAsStatement

  @applyRequiredParens: applyRequiredParens = (expr) -> "(#{expr})"
  applyRequiredParens: applyRequiredParens

  @applyParens: applyParens = (expr) ->
    if expr.match
        ///i
          ^(

          \([^)]*\) |

          \[[^\]]*\] |

          ([!~-]*[_a-z0-9.]*)(\([^)]*\))?

          )$
      expr
    else
      "(#{expr})"

  applyParens: applyParens

  ##########################
    VALIDATION
  ##########################
  validate: ->

  validateAll: ->
    try
      @validate()
    catch e
      throw @parseTreeNode.parser.generateCompileError {}
        failureOffset: @sourceOffset
        errorType: "Validation"
        e.message
        e.info
    each child in @children do child.validateAll()
    @

  ##########################
    SCOPE
  ##########################
  updateScope: (@scope) ->
    each child in @children do child.updateScope @scope
