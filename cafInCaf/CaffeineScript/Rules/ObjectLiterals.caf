import &StandardImport, &CaffeineEight, &StnRegistry
import Extensions

->
  @rule
    object:
      :implicitObject
      :explicitObject

    objectLiteralBlock: IndentBlocks.getPropsToSubparseToEolAndBlock
      rule: :explicitObjectBlock

    explicitObjectBlock:
      ""
        end*
        singleOrMultilineExplicitObject
        end*

    singleOrMultilineExplicitObject:
      :multilineExplicitObject
      :oneLineExplicitObject

  @rule

    implicitObject:
      "props:propertyList"

    oneLineExplicitObject:
      "props:explicitPropertyList"

    explicitObject:
      "'{}' _? props:explicitPropertyList"
      "'{}' _? props:objectLiteralBlock"
      "'{}'"

    bracketedObject: "openCurly_ props:explicitPropertyList _closeCurly"

    multilineImplicitObject:
      ""
        !implicitObjectWithTwoOrMorePropsOnOneLine
        valuePropWithComplexExpression
        multilineImplicitObjectExtension+


    multilineExplicitObject:
      ""
        explicitObjectLine
        multilineExplicitObjectExtension+

    {}
      getStn: ~>
        children = array m in @getMatchStns()
          if m instanceof ObjectStn.class
            m.children
          else
            m

        ObjectStn children

  @rule
    explicitObjectLine:
      :oneLineExplicitObject

      ""
        !implicitObjectWithTwoOrMorePropsOnOneLine
        explicitValuePropWithComplexExpression

    multilineImplicitObjectExtension:
      "end+ !implicitObjectWithTwoOrMorePropsOnOneLine valuePropWithComplexExpression"

    multilineExplicitObjectExtension:
      "end+ explicitObjectLine"

    implicitObjectWithTwoOrMorePropsOnOneLine:
      "literalProp _ propertyList"
      "valueProp _comma_optionalNewLine propertyList"

    explicitPropertyList:
      "valueProp optionalComma explicitPropertyList"
      "structurableProp _comma_optionalNewLine? explicitPropertyList"
      "explicitValuePropWithComplexExpression"

    propertyList:
      "valueProp optionalComma propertyList"
      "valuePropWithComplexExpression"

    implicitObjectStart: "" propName propNameEndColon

  @rule
    propNameEndColon:
      "" /:/ _?
      "" _ /:/ _

  @rule
    literalProp:  "propName propNameEndColon propValue:literal"
    valueProp:    "propName propNameEndColon propValue:singleValueOrImplicitArrayWithoutImplicitObjects"

    valuePropWithComplexExpression:
      "propName propNameEndColon propValue:singleValueOrImplicitArrayWithoutImplicitObjects"

    {}
      name: "literalObjectProperty"
      stnFactory: :ObjectPropValueStn

  @rule
    explicitValuePropWithComplexExpression:
      :valuePropWithComplexExpression
      :structurableProp

    structurableProp:
      :expression
      stnFactory: :ObjectPropValueStn

  @rule
    propName: "computedPropName"
    computedPropName:
      pattern: "openBracket_ expression _closeBracket"
      stnFactory: "ObjectLiteralAccessorStn"

  @rule
    thisPropName: "/@/ propNameExtension*"
    {}
      stnFactory: :ThisStn
      stnProps: ~> identifier: @propNameExtension?.toString()

  @rule
    propName:
      "" !/then\\s/ thisPropName &propNameEndColon
    {}
      stnFactory: "ObjectPropNameStn"
      stnProps: ~>
        isThisProp: true

  @rule
    propNameExtension:
      "" /:*/ unquotedPropNameToken &propNameEndColon

  @rule
    propName:
      "" !regExpLiteral !/then\\s/ unquotedPropNameToken &propNameEndColon propNameExtension*
    {}
      stnFactory: :ObjectPropNameStn
      stnProps: ~>
        value: @toString()
        isThisProp: false

  @rule
    propName:
      "" quotedString:stringLiteral &propNameEndColon

    {}
      stnFactory: :ObjectPropNameStn
      stnProps: ~>
        value: @toString()
        isThisProp: false
